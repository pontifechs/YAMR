buildscript {
    ext.kotlin_version = '1.0.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    signingConfigs {
        config {
            keyAlias 'android'
            storeFile file('/home/matt/android.jks')
            keyPassword '9876ar25'
            storePassword '9876ar25'
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "ninja.dudley.yamr"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName getVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(
                            output.outputFile.parent,
                            output.outputFile.name.replace("app", "YAMR")
                                    .replace("-release", "")
                                    .replace(".apk", "-${variant.versionName}.apk"))

                }
            }
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }


}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:support-v4:23.1.1'
    compile 'org.jsoup:jsoup:1.7.2'
    compile 'org.mozilla:rhino:1.7.7'
    compile 'commons-io:commons-io:2.4'
    compile 'ch.acra:acra:4.6.2'
}
repositories {
    mavenCentral()
}
